// SPDX-License-Identifier: GPL-2.0-or-later
pragma solidity >=0.5.0;

/// @notice Superset interface covering all entry points PassthroughVault may call on an underlying vault.
///
/// @dev    No single underlying vault type implements this entire interface. Which subset is live
///         depends on the vault being wrapped:
///
///         - SyncDepositVault:  sync-deposit functions + async-redeem functions.
///                              Async-deposit functions (requestDeposit, deposit(3), mint(3)) revert.
///
///         - AsyncVault:        async-deposit functions + async-redeem functions.
///                              Sync-deposit functions (deposit(2), mint(2), previewDeposit, previewMint) revert.
///
///         Deployers must know which underlying type they are wrapping and must only expose the
///         corresponding PassthroughVault flows to investors. Calling an unsupported flow will revert
///         in the underlying vault.
interface IUnderlyingVault {
    function asset() external view returns (address);
    function share() external view returns (address);
    function totalAssets() external view returns (uint256);
    function convertToShares(uint256 assets) external view returns (uint256);
    function convertToAssets(uint256 shares) external view returns (uint256);

    // Sync deposit (SyncDepositVault only)
    function deposit(uint256 assets, address receiver) external returns (uint256 shares);
    function mint(uint256 shares, address receiver) external returns (uint256 assets);
    function previewDeposit(uint256 assets) external view returns (uint256 shares);
    function previewMint(uint256 shares) external view returns (uint256 assets);
    function maxDeposit(address receiver) external view returns (uint256);
    function maxMint(address receiver) external view returns (uint256);

    // Async deposit (AsyncVault only)
    function requestDeposit(uint256 assets, address controller, address owner) external returns (uint256 requestId);
    function deposit(uint256 assets, address receiver, address controller) external returns (uint256 shares);
    function mint(uint256 shares, address receiver, address controller) external returns (uint256 assets);

    // Async redeem (both vault types)
    function requestRedeem(uint256 shares, address controller, address owner) external returns (uint256 requestId);
    function redeem(uint256 shares, address receiver, address controller) external returns (uint256 assets);
    function maxRedeem(address controller) external view returns (uint256);
    function maxWithdraw(address controller) external view returns (uint256);
}
